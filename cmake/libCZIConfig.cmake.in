# SPDX-FileCopyrightText: 2025 Carl Zeiss Microscopy GmbH
#
# SPDX-License-Identifier: MIT

@PACKAGE_INIT@

# This module is automatically installed by CMake and vcpkg when the libCZI package is installed.
# It is included via `find_package(libCZI CONFIG REQUIRED)` in downstream projects.
#
# Purpose:
#   - Provide a consistent and user-friendly interface to downstream users
#   - Ensure dependencies are found

# Flags baked in at install time:
set(LIBCZI_BUILD_AZURESDK_BASED_STREAM @LIBCZI_BUILD_AZURESDK_BASED_STREAM@)
set(LIBCZI_BUILD_CURL_BASED_STREAM     @LIBCZI_BUILD_CURL_BASED_STREAM@)

include(CMakeFindDependencyMacro)

# Find all required dependencies that libCZI expects clients to link against.
# These must also be declared in vcpkg.json dependencies to work properly across all platforms.
find_dependency(zstd CONFIG REQUIRED)
find_dependency(Eigen3 CONFIG REQUIRED)

if(LIBCZI_BUILD_AZURESDK_BASED_STREAM)
  find_dependency(azure-core-cpp CONFIG)
  find_dependency(azure-identity-cpp CONFIG)
  find_dependency(azure-storage-common-cpp CONFIG)
  find_dependency(azure-storage-blobs-cpp CONFIG)
endif()

if(LIBCZI_BUILD_CURL_BASED_STREAM)
  find_dependency(CURL CONFIG)
endif()

# Import the actual targets provided by the build (static or shared)
include("${CMAKE_CURRENT_LIST_DIR}/libCZITargets.cmake")
